
RAMAD1  equ     0F342H                  ; slotid DOS ram page 1
RAMAD2  equ     0F343H                  ; slotid DOS ram page 2
BDOS    equ     5
ENASLT  equ     00024H

        CALL    SAVPG1

        LD      DE,FCB
        LD      C,16H
        CALL    BDOS
        OR      A
        JP      NZ,ERROR

        LD      A,1
        LD      (PAGE),A
        LD      B,16
NXTPG1: PUSH    BC
        LD      A,16
        SUB     B
        LD      (BLOCK),A
        LD      C,A
        LD      A,(07FFFH)
        AND     11110000B
        OR      C
        LD      (07FFFH),A
        LD      DE,04000H
        CALL    DUMP
        LD      A,(07FFFH)
        AND     11110000B
        LD      (07FFFH),A
        POP     BC
        DJNZ    NXTPG1

        LD      A,2
        LD      (PAGE),A
        LD      B,16
NXTPG2: PUSH    BC
        LD      A,16
        SUB     B
        LD      (BLOCK),A
        LD      C,A
        LD      A,(07FFFH)
        AND     11110000B
        OR      C
        LD      (07FFFH),A
        LD      DE,08000H
        CALL    DUMP
        LD      A,(07FFFH)
        AND     11110000B
        LD      (07FFFH),A
        POP     BC
        DJNZ    NXTPG2

        LD      DE,FCB
        LD      C,10H
        CALL    BDOS
        OR      A
        JP      NZ,ERROR

        CALL    RSTPG1
        RET

DUMP:   
        LD      C,1AH
        CALL    BDOS

        LD      DE,STRBL1
        LD      C,09H
        CALL    BDOS
        LD      A,(PAGE)
        CALL    PRTBYT
        LD      DE,STRBL2
        LD      C,09H
        CALL    BDOS
        LD      A,(BLOCK)
        CALL    PRTBYT
        LD      DE,STRBL3
        LD      C,09H
        CALL    BDOS

        LD      HL,1
        LD      (FCB+14),HL
        LD      HL,04000H
        LD      DE,FCB
        LD      C,26H
        CALL    BDOS
        OR      A
        RET     Z

ERROR:  
        CALL    RSTPG1
        LD      DE,STRERR
        LD      C,09H
        CALL    BDOS
        LD      C,07H
        CALL    BDOS
        JP      0

SAVPG1:
        DI
        LD      A,(RAMAD1)
        LD      (SAVAD1),A
        LD      A,08FH
        LD      (RAMAD1),A
        LD      H,HIGH 04000H
        CALL    ENASLT
        LD      A,(RAMAD2)
        LD      (SAVAD2),A
        LD      A,08FH
        LD      (RAMAD2),A
        LD      H,HIGH 08000H
        CALL    ENASLT
        EI
        RET

RSTPG1:
        DI
        LD      A,(SAVAD1)
        LD      (RAMAD1),A
        LD      H,HIGH 04000H
        CALL    ENASLT
        LD      A,(SAVAD2)
        LD      (RAMAD2),A
        LD      H,HIGH 08000H
        CALL    ENASLT
        EI
        RET

;	  Subroutine output byte in HEX to screen
;	     Inputs  ________________________
;	     Outputs ________________________

PRTBYT:	PUSH	AF
        RRCA	
        RRCA	
        RRCA	
        RRCA	
	CALL	PRTNIB                  ; output nibble in HEX to screen
        POP	AF

;	  Subroutine output nibble in HEX to screen
;	     Inputs  ________________________
;	     Outputs ________________________

PRTNIB:	PUSH	AF
        AND	0FH
        CP	0AH
        SBC	A,69H
        DAA	
        LD      C,6
        LD      E,A
        CALL    BDOS
        POP	AF
        RET	

STRBL1: DEFB    "Dumping page "
        DEFB    "$"
STRBL2: DEFB    " block "
        DEFB    "$"
STRBL3: DEFB    "...",13,10
        DEFB    "$"

STRERR: DEFB    "Error dumping the firmware!",13,10
        DEFB    "$"

FCB:    DEFB    0
        DEFB    "FIRMWAREROM"
        DEFW    0
        DEFW    0
        DEFB    0,0,0,0
        DEFW    0
        DEFW    0
        DEFB    0
        DEFB    0
        DEFW    0
        DEFW    0
        DEFW    0
        DEFB    0
        DEFB    0,0,0,0

SAVAD1: DEFB    0
SAVAD2: DEFB    0
PAGE:   DEFB    0
BLOCK:  DEFB    0
